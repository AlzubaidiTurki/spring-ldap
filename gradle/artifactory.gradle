apply plugin: "com.jfrog.artifactory"

def isSnapshot = version?.endsWith("-SNAPSHOT")
def isMilestone = version?.matches('^.*[.-]M\\d+$') || version?.matches('^.*[.-]RC\\d+$')

artifactory {
    contextUrl = 'https://repo.spring.io'
    publish {
        repository {
            repoKey = 'libs-release-local'
            if (isSnapshot) {
                repoKey = 'libs-snapshot-local'
            } else if (isMilestone) {
                repoKey = 'libs-milestone-local'
            }
            if(project.hasProperty('artifactoryUsername')) {
                username = artifactoryUsername
                password = artifactoryPassword
            }
        }
    }
}

artifactoryPublish {
    publishConfigs 'archives'
    publishIvy false
    properties = [
            'bintray.package': "${project.group}:spring-security",
            'bintray.version': "${project.version}"
    ]
}

artifactory {
    publish {
        defaults {
            publishConfigs('archives')
        }
    }
}
